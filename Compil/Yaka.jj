/*
 *
 *       Auteur: Pedrono Marie-Jo
 *
 * 	création : 13 février 2002
 *
 *     nature : projet etudiant
 *
 */


options{
	JDK_VERSION="1.5";	
}

PARSER_BEGIN(Yaka)


package Compil;

import Compil.Ident.*;
import Compil.Expression.*;
import Compil.Declaration.*;


public class Yaka {
	public static Declaration declaration;
	public static TabIdent tabIdent;
	public static Expression expression;
	public static TypeVar type;
	//public static YVM yvm;

  public static void main(String args[]) { 
    Yaka analyseur;
    java.io.InputStream input;

    if (args.length==1) {
      System.out.print(args[args.length-1] + ": ");
      try {
        input = new java.io.FileInputStream(args[args.length-1]+".yaka");
      } catch (java.io.FileNotFoundException e) {
        System.out.println("Fichier introuvable.");
        return;
      }
    } else if (args.length==0) {
      System.out.println("Lecture sur l'entree standard...");
      input = System.in;
    } else {
      System.out.println("Usage: java Gram [fichier]");
      return;
    }
    try {
      analyseur = new Yaka(input);
      analyseur.analyse();
      System.out.println("analyse syntaxique reussie!");
    } catch (ParseException e) {
      String msg = e.getMessage();
      msg = msg.substring(0,msg.indexOf("\n"));
      System.out.println("Erreur de syntaxe : "+msg);
    }
  } 
}

PARSER_END(Yaka)
/***************************************/
/********** TOKEN DEFINITIONS **********/
/***************************************/

TOKEN_MGR_DECLS :
{public static String identLu,chaineLue;
 public static int entierLu;}

/*** Skip whitespace and comments ***/
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "(*"   : IN_COMMENT
}
<IN_COMMENT> MORE:
{
  < ~[] >
}
<IN_COMMENT> SKIP:
{
   < "*)" >  {SwitchTo(DEFAULT);} 
}


/* Mots réservés*/

TOKEN :
{
  < BOOLEEN: "BOOLEEN" >
| < VAR: "VAR" >
| < FAIRE: "FAIRE" >
| < SINON: "SINON" >
| < POUR: "POUR" >
| < SI: "SI" >
| < FSI: "FSI">
| < ENTIER: "ENTIER" >
| < RETOURNE: "RETOURNE" >
| < VRAI: "VRAI" >
| < TANTQUE: "TANTQUE" >
| <CONST : "CONST">
| <FAUX : "FAUX">
| <FAIT : "FAIT">
| <OU : "OU">
| <ET : "ET">
| <NON : "NON">
| <FONCTION : "FONCTION">
| <FFONCTION  : "FFONCTION">
| <PROGRAMME : "PROGRAMME" >
| <FPROGRAMME: "FPROGRAMME" >
| <PRINCIPAL : "PRINCIPAL">
| <FPRINCIPAL : "FPRINCIPAL">
| <ALORS : "ALORS">
| <ECRIRE : "ECRIRE">
| <LIRE : "LIRE">
| <ALALIGNE : "ALALIGNE">

}



/*** unites de base nombres, idents,  strings ***/
TOKEN  :
{
  < #chiffre : ["0"-"9"] >
| < entier : (<chiffre>)+ > 
	{ entierLu = Integer.parseInt(image.toString());
        }
| < #lettre: ["A"-"Z","a"-"z"] >
| < ident : <lettre> (<lettre> | <chiffre>)* >
	{identLu =image.toString();	
	}
| < chaine : "\"" (~["\""])* "\"" | "'" (~["'"])* "'" >
	{ chaineLue = image.toString();
	 }
}


/**************************************/
/********debut de la grammaire ********/
/**************************************/
void analyse() : {}
{
   <PROGRAMME> <ident>	
   bloc()
   <FPROGRAMME> 
}

void bloc() : {}{
 (declConst())*
 (declVar())*		
   suiteExpr() 
}

void declConst() : {}{
  <CONST>  defConst() ( "," defConst())* ";"
}

void defConst() : {}{
  <ident> "=" valConst() {declaration.declConst(YakaTokenManager.identLu, YakaTokenManager.entierLu);}
}

void valConst() : {}{
  <entier> 		
 | <ident>		
 | <VRAI> 		
 | <FAUX> 		
}


void declVar() : {} {
  <VAR> type()	
  <ident> {declaration.declVar(YakaTokenManager.identLu, type); }		
  ("," 
      <ident>  {declaration.declVar(YakaTokenManager.identLu, type);}
  )* ";"
	
}


void type() :{} {
   <ENTIER>  {type = TypeVar.INT;}			
 | <BOOLEEN> {type = TypeVar.BOOL;}	
}
/*
 * Syntaxe des instructions.
 */
void suiteExpr() : {}
{	
	expression() { expression.evaluate();}
	(";" (expression() { expression.evaluate();})? )*
	//(expression()
	//(";" (expression())? )*)?
}



/*
 * Expression .
 */
void expression() : {}
{
  simpleExpr() 
  (opRel() 
  simpleExpr() 
  )? 
}



void simpleExpr() : {}
{
   terme() 
   (opAdd()
   terme()	
   )* 
} 
void terme() : {}
{
  facteur() 
  (opMul() 
  facteur()	
  )*
}
void facteur() : {}
{  
	primaire() 
	|  opNeg() primaire() 
}

void primaire (): {}
{
   valeur()
 | "(" expression() ")" 
 
}

void valeur () : {} { 
  <entier> 	{expression.addImmediate(TypeVar.INT);}	
 | <ident> 	{expression.addIdent(YakaTokenManager.identLu);}	
 | <VRAI> 	{expression.addImmediate(TypeVar.BOOL);}	
 | <FAUX> 	{expression.addImmediate(TypeVar.BOOL);}	
}

void opRel() : {} {
   "=" 		{expression.addOp(TypeOp.ARLO);}
 | "<>" 	{expression.addOp(TypeOp.ARLO);}
 | "<" 		{expression.addOp(TypeOp.ARLO);}
 | "<=" 	{expression.addOp(TypeOp.ARLO);}
 | ">" 		{expression.addOp(TypeOp.ARLO);}
 | ">=" 	{expression.addOp(TypeOp.ARLO);}
} 

void opAdd() : {}
{
    "+"		{expression.addOp(TypeOp.ARITH2);}
  |  "-"  	{expression.addOp(TypeOp.ARITH2);}
  | <OU> 	{expression.addOp(TypeOp.LOGIQUE2);}
}

void opMul() : {}
{
  "*"		{expression.addOp(TypeOp.ARITH2);}
  | "/"		{expression.addOp(TypeOp.ARITH2);}
  | <ET>	{expression.addOp(TypeOp.LOGIQUE2);}
} 

void opNeg() : {}
{ 
   "-" 		{expression.addOp(TypeOp.ARITH1);}
 | <NON>	{expression.addOp(TypeOp.LOGIQUE1);}
}


