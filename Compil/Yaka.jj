/*
 *
 *       Auteur: Pedrono Marie-Jo
 *
 * 	création : 13 février 2002
 *
 *     nature : projet etudiant
 *
 */


options{
	JDK_VERSION="1.5";	
}

PARSER_BEGIN(Yaka)


package Compil;


import Compil.Expression.*;


public class Yaka {
	public static Declaration declaration;
	public static TabIdent tabIdent;
	public static Expression expression;
	public static Iteration iteration;
	public static int type; //prend ses valeurs dans YakaConstants
	public static YVM yvm;
	public static int nbErreurs;
	public static Conditionnelle conditionnelle;
	
	public static String afficherLigne() {
		return " a la ligne " + Yaka.token.beginLine + ", colonne " + Yaka.token.beginColumn;
	}
	
  public static void main(String args[]) { 
  	nbErreurs = 0;
    Yaka analyseur;
    java.io.InputStream input;
	System.out.println(args);
    if (args.length==1) {
      System.out.print(args[args.length-1] + ": ");
      try {
        input = new java.io.FileInputStream(args[args.length-1]+".yaka");
        yvm = new YVM(args[args.length-1]+".yvm");
      } catch (java.io.FileNotFoundException e) {
        System.out.println("Fichier introuvable.");
        return;
      }
    }
    else if (args.length==2) {
      System.out.print(args[args.length-1] + ": ");
      try {
        input = new java.io.FileInputStream(args[args.length-1]+".yaka");
        if (args[0].equals("asm"))
        	yvm = new YVMAsm(args[args.length-1]+".asm");
        else
        	yvm = new YVM(args[args.length-1]+".yvm");
      } catch (java.io.FileNotFoundException e) {
        System.out.println("Fichier introuvable.");
        return;
      }
    }
    else if (args.length==0) {
      System.out.println("Lecture sur l'entree standard...");
      input = System.in;
    } 
    
    else {
      System.out.println("Usage: java Gram [asm] [fichier]");
      return;
    }
    try {
      analyseur = new Yaka(input);
      declaration = new Declaration();
      expression = new Expression();
      tabIdent = new TabIdent();
      conditionnelle = new Conditionnelle();
      analyseur.analyse();
    }
    catch (ParseException e) {
      	String msg = e.getMessage();
     	msg = msg.substring(0,msg.indexOf("\n"));
     	System.out.println("Erreur de syntaxe : "+ msg);
    }
      if (nbErreurs == 0) {
      	System.out.println("Analyse syntaxique reussie!");
      }
      else System.out.println("Analyse terminée avec " + Yaka.nbErreurs + " erreurs. T_T");
          
  } 
}

PARSER_END(Yaka)
/***************************************/
/********** TOKEN DEFINITIONS **********/
/***************************************/

TOKEN_MGR_DECLS :
{public static String identLu,chaineLue;
 public static int entierLu;}

/*** Skip whitespace and comments ***/
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "(*"   : IN_COMMENT
}
<IN_COMMENT> MORE:
{
  < ~[] >
}
<IN_COMMENT> SKIP:
{
   < "*)" >  {SwitchTo(DEFAULT);} 
}


/* Mots réservés*/

TOKEN :
{
  < BOOLEEN: "BOOLEEN" >
| < VAR: "VAR" >
| < FAIRE: "FAIRE" >
| < SINON: "SINON" >
| < POUR: "POUR" >
| < SI: "SI" >
| < FSI: "FSI">
| < ENTIER: "ENTIER" >
| < RETOURNE: "RETOURNE" >
| < VRAI: "VRAI" >
| < TANTQUE: "TANTQUE" >
| <CONST : "CONST">
| <FAUX : "FAUX">
| <FAIT : "FAIT">
| <OU : "OU">
| <ET : "ET">
| <NON : "NON">
| <FONCTION : "FONCTION">
| <FFONCTION  : "FFONCTION">
| <PROGRAMME : "PROGRAMME" >
| <FPROGRAMME: "FPROGRAMME" >
| <PRINCIPAL : "PRINCIPAL">
| <FPRINCIPAL : "FPRINCIPAL">
| <ALORS : "ALORS">
| <ECRIRE : "ECRIRE">
| <LIRE : "LIRE">
| <ALALIGNE : "ALALIGNE">

}



/*** unites de base nombres, idents,  strings ***/
TOKEN  :
{
  < #chiffre : ["0"-"9"] >
| < entier : (<chiffre>)+ > 
	{ entierLu = Integer.parseInt(image.toString());
        }
| < #lettre: ["A"-"Z","a"-"z"] >
| < ident : <lettre> (<lettre> | <chiffre>)* >
	{identLu =image.toString();	
	}
| < chaine : "\"" (~["\""])* "\"" | "'" (~["'"])* "'" >
	{ chaineLue = image.toString();
	 }
}


/**************************************/
/********debut de la grammaire ********/
/**************************************/

void analyse() : {}
{
   <PROGRAMME> <ident>	{yvm.debutProg();}
   bloc()
   <FPROGRAMME> {yvm.finProg();}
}

void bloc() : {}{
 (declConst())*
 (declVar())* {yvm.ouvrePrinc();}	
   suiteInstr() 
}

void declConst() : {}{
  <CONST>  defConst() ( "," defConst())* ";"
}

void defConst() : {}{
  <ident> "=" valConst()
}

void valConst() : {String ident1 = YakaTokenManager.identLu;}{
  <entier> 	 {declaration.declConst(ident1, YakaConstants.ENTIER, YakaTokenManager.entierLu);}
 | <ident>	 {declaration.declConst(ident1, YakaTokenManager.identLu);}
 | <VRAI> 	 {declaration.declConst(ident1, YakaConstants.BOOLEEN, YVM.vrai);}
 | <FAUX> 	 {declaration.declConst(ident1, YakaConstants.BOOLEEN, YVM.faux);}
}


void declVar() : {} {
  <VAR> type()	
  <ident> { declaration.declVar(YakaTokenManager.identLu, type); }
  ("," 
      <ident>  {declaration.declVar(YakaTokenManager.identLu, type);}
  )* ";"
	
}


void type() :{} {
   <ENTIER>  {type = YakaConstants.ENTIER;}			
 | <BOOLEEN> {type = YakaConstants.BOOLEEN;}	
}
/*
 * Syntaxe des instructions.
 */
void suiteExpr() : {}
{	
	expression() 
	(";" (expression() )? )*
}



void suiteInstr() : {}
{
    instruction() 
    (";" (instruction())? )*
}

void instruction() : {}
{
      affectation()
    | lecture()
    | ecriture()
    | iteration()
    | conditionnelle()
}

/*
 *Iteration
 */
void iteration() : {} 
{
	<TANTQUE> {iteration.debutTantQue();}
	 expression() 
	<FAIRE> suiteInstr()
	<FAIT> {iteration.finTantQue();}
}

/*
 * Conditionnelle
 */

void conditionnelle() : {} 
{
	<SI> expression() {conditionnelle.verifieTypeExpr();}
	<ALORS> (expression()";")*
	(<SINON> (expression()";")*)?
	<FSI>
}

/*
 *Affectations
 */

void affectation() : {}
{
    <ident> {String ident = YakaTokenManager.identLu;expression.stockeAffectation(ident);}
    "=" 
    expression() {expression.evaluerAffectation();yvm.affecter(ident);}
}

/*
 * Entrees/sorties
 */
 
void lecture() : {}
{
    <LIRE> 
	"("
	<ident> { yvm.lireEnt(YakaTokenManager.identLu) ;}
	")"
}

void ecriture() : {}
{
    (<ECRIRE> "(" 
        (expression() { yvm.ecrireExpr() ;}
        | <chaine> {yvm.ecrireChaine(YakaTokenManager.chaineLue) ;})
		")")
    | <ALALIGNE> {yvm.aLaLigne() ;}
}

/*
 * Expression .
 */
void expression(): {}
{
  (simpleExpr() 
  (opRel() 
  simpleExpr() { yvm.lireOp(expression.dernierOperateur()); expression.evaluate();}
  )?) 
}



void simpleExpr() : {}
{
   terme() 
   (opAdd()
   terme() { yvm.lireOp(expression.dernierOperateur()); expression.evaluate();}
   )* 
} 
void terme() : {}
{
  facteur() 
  (opMul() 
  facteur() { yvm.lireOp(expression.dernierOperateur()); expression.evaluate();}
  )*
}
void facteur() : {}
{  
	primaire() 
	|  opNeg() primaire() {yvm.lireOp(expression.dernierOperateur()); expression.evaluate();}
}

void primaire (): {}
{
   valeur()
 | "(" expression() ")" 
 
}

void valeur (): {} { 
   <entier> { expression.addImmediate(YakaConstants.ENTIER); yvm.lireImmediat(YakaTokenManager.entierLu);}
 | <ident> 	{ expression.addIdent(YakaTokenManager.identLu); yvm.lireConstOuVar(YakaTokenManager.identLu);}
 | <VRAI> 	{ expression.addImmediate(YakaConstants.BOOLEEN); yvm.lireImmediat(YVM.vrai);}
 | <FAUX> 	{ expression.addImmediate(YakaConstants.BOOLEEN); yvm.lireImmediat(YVM.faux);}
}

void opRel() : {} {
   "=" 		{expression.addOp(Op.EGAL);}
 | "<>" 	{expression.addOp(Op.DIFF);}
 | "<" 		{expression.addOp(Op.INFEG);}
 | "<=" 	{expression.addOp(Op.INFEG);}
 | ">" 		{expression.addOp(Op.SUP);}
 | ">=" 	{expression.addOp(Op.SUPEG);}
} 

void opAdd() : {}
{
    "+"		{expression.addOp(Op.ADD);}
  |  "-"  	{expression.addOp(Op.SOUS);}
  | <OU> 	{expression.addOp(Op.OU);}
}

void opMul() : {}
{
  "*"		{expression.addOp(Op.MUL);}
  | "/"		{expression.addOp(Op.DIV);}
  | <ET>	{expression.addOp(Op.ET);}
} 

void opNeg() : {}
{ 
   "-" 		{expression.addOp(Op.NEG);}
 | <NON>	{expression.addOp(Op.NON);}
}


