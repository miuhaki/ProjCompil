/*
 *
 *       Auteur: Pedrono Marie-Jo
 *
 * 	création : 13 février 2002
 *
 *     nature : projet etudiant
 *
 */


options{
	JDK_VERSION="1.5";	
}

PARSER_BEGIN(Yaka)


package Compil;


import Compil.Expression.*;


public class Yaka {
	public static Declaration declaration;
	public static TabIdent tabIdent;
	public static Expression expression;
	public static int type; //prend ses valeurs dans YakaConstants
	public static YVM yvm;

  public static void main(String args[]) { 
    Yaka analyseur;
    java.io.InputStream input;

    if (args.length==1) {
      System.out.print(args[args.length-1] + ": ");
      try {
        input = new java.io.FileInputStream(args[args.length-1]+".yaka");
        yvm = new YVM(args[args.length-1]+".yvm");
      } catch (java.io.FileNotFoundException e) {
        System.out.println("Fichier introuvable.");
        return;
      }
    } else if (args.length==0) {
      System.out.println("Lecture sur l'entree standard...");
      input = System.in;
    } else {
      System.out.println("Usage: java Gram [fichier]");
      return;
    }
    try {
      analyseur = new Yaka(input);
      declaration = new Declaration();
      expression = new Expression();
      tabIdent = new TabIdent();
      analyseur.analyse();
      System.out.println("analyse syntaxique reussie!");
    }
    catch (ParseException e) {
      	String msg = e.getMessage();
     	msg = msg.substring(0,msg.indexOf("\n"));
     	System.out.println("Erreur de syntaxe : "+ msg);
    }
    catch (TabIdent.NoSuchKeyException e) {
		String msg = e.getMessage();
     	System.out.println(msg);
    }
    catch (TabIdent.KeyAlreadyThereException e) {
		String msg = e.getMessage();
     	System.out.println(msg);
    }
      
  } 
}

PARSER_END(Yaka)
/***************************************/
/********** TOKEN DEFINITIONS **********/
/***************************************/

TOKEN_MGR_DECLS :
{public static String identLu,chaineLue;
 public static int entierLu;}

/*** Skip whitespace and comments ***/
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "(*"   : IN_COMMENT
}
<IN_COMMENT> MORE:
{
  < ~[] >
}
<IN_COMMENT> SKIP:
{
   < "*)" >  {SwitchTo(DEFAULT);} 
}


/* Mots réservés*/

TOKEN :
{
  < BOOLEEN: "BOOLEEN" >
| < VAR: "VAR" >
| < FAIRE: "FAIRE" >
| < SINON: "SINON" >
| < POUR: "POUR" >
| < SI: "SI" >
| < FSI: "FSI">
| < ENTIER: "ENTIER" >
| < RETOURNE: "RETOURNE" >
| < VRAI: "VRAI" >
| < TANTQUE: "TANTQUE" >
| <CONST : "CONST">
| <FAUX : "FAUX">
| <FAIT : "FAIT">
| <OU : "OU">
| <ET : "ET">
| <NON : "NON">
| <FONCTION : "FONCTION">
| <FFONCTION  : "FFONCTION">
| <PROGRAMME : "PROGRAMME" >
| <FPROGRAMME: "FPROGRAMME" >
| <PRINCIPAL : "PRINCIPAL">
| <FPRINCIPAL : "FPRINCIPAL">
| <ALORS : "ALORS">
| <ECRIRE : "ECRIRE">
| <LIRE : "LIRE">
| <ALALIGNE : "ALALIGNE">

}



/*** unites de base nombres, idents,  strings ***/
TOKEN  :
{
  < #chiffre : ["0"-"9"] >
| < entier : (<chiffre>)+ > 
	{ entierLu = Integer.parseInt(image.toString());
        }
| < #lettre: ["A"-"Z","a"-"z"] >
| < ident : <lettre> (<lettre> | <chiffre>)* >
	{identLu =image.toString();	
	}
| < chaine : "\"" (~["\""])* "\"" | "'" (~["'"])* "'" >
	{ chaineLue = image.toString();
	 }
}


/**************************************/
/********debut de la grammaire ********/
/**************************************/
void analyse() throws TabIdent.KeyAlreadyThereException, TabIdent.NoSuchKeyException: {}
{
   <PROGRAMME> <ident>	{yvm.debutProg();}
   bloc()
   <FPROGRAMME> {yvm.finProg();}
}

void bloc() throws TabIdent.KeyAlreadyThereException, TabIdent.NoSuchKeyException : {}{
 (declConst())*
 (declVar())* {yvm.ouvrePrinc();}	
   suiteExpr() 
}

void declConst() throws TabIdent.KeyAlreadyThereException, TabIdent.NoSuchKeyException : {}{
  <CONST>  defConst() ( "," defConst())* ";"
}

void defConst() throws TabIdent.KeyAlreadyThereException, TabIdent.NoSuchKeyException : {}{
  <ident> "=" valConst()
}

void valConst() throws TabIdent.KeyAlreadyThereException, TabIdent.NoSuchKeyException : {String ident1 = YakaTokenManager.identLu;}{
  <entier> 	 {declaration.declConst(ident1, YakaConstants.ENTIER, YakaTokenManager.entierLu);}
 | <ident>	 {declaration.declConst(ident1, YakaTokenManager.identLu);}
 | <VRAI> 	 {declaration.declConst(ident1, YakaConstants.BOOLEEN, YakaConstants.VRAI);}
 | <FAUX> 	 {declaration.declConst(ident1, YakaConstants.BOOLEEN, YakaConstants.FAUX);}
}


void declVar() throws TabIdent.KeyAlreadyThereException : {} {
  <VAR> type()	
  <ident> {declaration.declVar(YakaTokenManager.identLu, type); }		
  ("," 
      <ident>  {declaration.declVar(YakaTokenManager.identLu, type);}
  )* ";"
	
}


void type() :{} {
   <ENTIER>  {type = YakaConstants.ENTIER;}			
 | <BOOLEEN> {type = YakaConstants.BOOLEEN;}	
}
/*
 * Syntaxe des instructions.
 */
void suiteExpr() throws TabIdent.KeyAlreadyThereException, TabIdent.NoSuchKeyException : {}
{	
	expression() 
	(";" (expression() )? )*
	//(expression()
	//(";" (expression())? )*)?
}

/*
 *Affectations
 */

void suiteInstr() throws TabIdent.KeyAlreadyThereException, TabIdent.NoSuchKeyException: {}
{
    instruction() 
    (";" (instruction())? )*
}

void instruction() throws TabIdent.KeyAlreadyThereException, TabIdent.NoSuchKeyException: {}
{
      affectation()
    | lecture()
    | ecriture()
}

void affectation() throws TabIdent.KeyAlreadyThereException, TabIdent.NoSuchKeyException : {}
{
    <ident> {expression.stockeAffectation(YakaTokenManager.identLu);} 
    "=" 
    expression() {expression.evaluerAffectation();}
}

/*
 * Entrees/sorties
 */
 
void lecture() throws TabIdent.NoSuchKeyException: {}
{
    <LIRE> 
	"("
	<ident> {yvm.lireEnt(YakaTokenManager.identLu) ;}
	")"
}

void ecriture() throws TabIdent.KeyAlreadyThereException, TabIdent.NoSuchKeyException: {}
{
    (<ECRIRE> "(" 
        (expression() {/*yvm.ecrireExpr() ;*/}
        | <chaine> {yvm.ecrireChaine(YakaTokenManager.chaineLue) ;})
		")")
    | <ALALIGNE> {yvm.aLaLigne() ;}
}

/*
 * Expression .
 */
void expression() throws TabIdent.KeyAlreadyThereException, TabIdent.NoSuchKeyException: {}
{
  (simpleExpr() 
  (opRel() 
  simpleExpr() { yvm.lireOp(expression.dernierOp()); expression.evaluate();} 
  )?) 
}



void simpleExpr() throws TabIdent.KeyAlreadyThereException, TabIdent.NoSuchKeyException: {}
{
   terme() 
   (opAdd()
   terme() { yvm.lireOp(expression.dernierOp()); expression.evaluate();}
   )* 
} 
void terme() throws TabIdent.KeyAlreadyThereException, TabIdent.NoSuchKeyException: {}
{
  facteur() 
  (opMul() 
  facteur() { yvm.lireOp(expression.dernierOp()); expression.evaluate();}
  )*
}
void facteur() throws TabIdent.KeyAlreadyThereException, TabIdent.NoSuchKeyException: {}
{  
	primaire() 
	|  opNeg() primaire() { yvm.lireOp(expression.dernierOp()); expression.evaluate();}
}

void primaire () throws TabIdent.KeyAlreadyThereException, TabIdent.NoSuchKeyException: {}
{
   valeur()
 | "(" expression() ")" 
 
}

void valeur () throws TabIdent.NoSuchKeyException, TabIdent.KeyAlreadyThereException : {Boolean b = false;} { 
   <entier> {expression.addImmediate(YakaConstants.ENTIER); yvm.lireImmediat(YakaTokenManager.entierLu);}	
 | <ident> 	{expression.addIdent(YakaTokenManager.identLu); yvm.lireConstOuVar(YakaTokenManager.identLu);}
 | <VRAI> 	{expression.addImmediate(YakaConstants.BOOLEEN); yvm.lireImmediat(YakaConstants.VRAI);}	
 | <FAUX> 	{expression.addImmediate(YakaConstants.BOOLEEN); yvm.lireImmediat(YakaConstants.FAUX);}
}

void opRel() : {} {
   "=" 		{expression.addOp(Op.EGAL);}
 | "<>" 	{expression.addOp(Op.DIFF);}
 | "<" 		{expression.addOp(Op.INFEG);}
 | "<=" 	{expression.addOp(Op.INFEG);}
 | ">" 		{expression.addOp(Op.SUP);}
 | ">=" 	{expression.addOp(Op.SUPEG);}
} 

void opAdd() : {}
{
    "+"		{expression.addOp(Op.ADD);}
  |  "-"  	{expression.addOp(Op.SOUS);}
  | <OU> 	{expression.addOp(Op.OU);}
}

void opMul() : {}
{
  "*"		{expression.addOp(Op.MUL);}
  | "/"		{expression.addOp(Op.DIV);}
  | <ET>	{expression.addOp(Op.ET);}
} 

void opNeg() : {}
{ 
   "-" 		{expression.addOp(Op.NEG);}
 | <NON>	{expression.addOp(Op.NON);}
}


